#include "monsters/backpackdrops.txt"

#include "monsters/zombies.txt"

actor QuakeImp: DoomImp replaces DoomImp
{
    Health 80
    GibHealth -30

    States
    {
      Pain:
        TROO H 0 A_Jump(235, 2)
        TROO H 0 A_JumpIfHealthLower(30, "Knockdown")
        TROO H 2
        TROO H 2 A_Pain
        goto Super::See

      DownPain:
        TROO M 0 A_Pain
        goto Down

      Death:
        TROO A 0 A_JumpIfInventory("KnockedDown", 1, "DownDeath")
        TROO I 8
        TROO J 8 A_Scream
        TROO K 6
        TROO L 6 A_NoBlocking
        TROO M -1
        stop
      
      DownDeath:
        TROO M 0 A_TakeInventory("KnockedDown")
        TROO M 0 A_Scream
        TROO LKL 6
        TROO M -1
        stop
      
      Down:
        TROO M 4 A_SetShootable
        TROO M 0 HealThing(1)
        TROO M 0 A_JumpIfHealthLower(40, "Down")
        goto BackUp

      XDeath:
        TROO M 0 A_TakeInventory("KnockedDown")
        goto Super::XDeath

      Knockdown:
        TROO I 8 A_ChangeFlag("NOPAIN", 1) 
        TROO J 8
        TROO K 6
        TROO K 0 A_GiveInventory("KnockedDown")
        TROO L 6 A_NoBlocking
        TROO M 4
        goto Down

      BackUp:
        TROO M 0 A_UnsetFloorClip
        TROO M 0 A_PlaySound("misc/gibbed") 
        TROO M 4 A_TakeInventory("KnockedDown")
        TROO LK 6
        TROO JI 8
        TROO A 0 A_ChangeFlag("NOPAIN", 0)
        TROO A 0 A_ClearTarget
        goto Super::Spawn
    } 
}

actor KnockedDown: Inventory {}

actor QuakeDemon: Demon replaces Demon
{
    Health 75
}

actor QuakeSpectre: QuakeDemon replaces Spectre
{
    +SHADOW
    
    RenderStyle OptFuzzy
    Alpha 0.5
    
    SeeSound "spectre/sight"
    AttackSound "spectre/melee"
    PainSound "spectre/pain"
    DeathSound "spectre/death"
    ActiveSound "spectre/active"

    Obituary "$OB_SPECTREHIT"
}
